plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "company.vk.lection09"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        dummy {
            storeFile file("$rootDir/danger/dummy.jks")
                storePassword 'qwerty'
                keyAlias 'dummy'
                keyPassword 'qwerty'
            }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.dummy
        }

        release {
            signingConfig signingConfigs.dummy
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        unsafe {
            initWith release
            debuggable true
            matchingFallbacks = ['release']
        }
    }

    flavorDimensions "structure"
    productFlavors {
        simple {
            dimension "structure"
        }

        advanced {
            dimension "structure"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }


}

dependencies {
    implementation "androidx.startup:startup-runtime:1.1.1"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.fragment:fragment-ktx:1.4.1"


    implementation project(":base:common")

    implementation project(":base:loggers:api")
    debugImplementation project(":base:loggers:simple")
    unsafeImplementation project(":base:loggers:simple")
    releaseImplementation project(":base:loggers:empty")

    implementation project(":base:images:api")
    implementation project(":base:images:glide")

    implementation project(":features:videos:api")
    simpleImplementation project(":base:films:simple")
    advancedImplementation project(":base:films:retrofit")

    testImplementation 'junit:junit:4.+'
    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
}